namespace mandelbrot;

node S {

	markers = {
		9: ' ', 8: '.', 7: ':', 6: '+', 5: '@',
		4: '=', 3: '*', 2: '%', 1: '$', 0: '#'
	};
	max_steps = 10;

	s = '';
	iteration_count = 0;

	width = (width == null) ? 100 : integer(width);
	height = (height == null) ? 49 : integer(height);

	max_dist = 6.0;
	x0 = -1.5;
	x1 =  0.5;
	y0 = -1.0;
	y1 =  1.0;

	dx = (x1 - x0) / (width - 1);
	dy = (y1 - y0) / (height - 1);

	_i = 0;
	i: while (_i < height)
	{
		y = y0 + dy * _i;

		_j = 0;
		j: while (_j < width)
		{
			x = x0 + dx * _j;

			_k = 0;
			Zx = 0.0;
			Zy = 0.0;
			k: while (_k < max_steps)
			{
				_Zx = Zx*Zx - Zy*Zy + x;
				_Zy = 2.0*Zx*Zy + y;
				if (_Zx*_Zx + _Zy*_Zy >= max_dist)
					break k;
				Zx = _Zx;
				Zy = _Zy;

				++ iteration_count;
				++ _k;
			}

			s = s + markers[_k % max_steps];
			++ _j;
		}

		s = s + '\n';
		++ _i;
	}

};
