namespace mandelbrot;

function render (width, height)
{
    max_steps = 10;

    s = '';
    iteration_count = 0;

    max_dist = 6.0;
    x0 = -1.5;
    x1 =  0.5;
    y0 = -1.0;
    y1 =  1.0;

    dx = (x1 - x0) / (width - 1);
    dy = (y1 - y0) / (height - 1);

    i: for ( _i = 0; _i < height; ++ _i )
    {
        y = y0 + dy * _i;

        j: for ( _j = 0; _j < width; ++ _j )
        {
            x = x0 + dx * _j;

            _k = 0;
            Zx = 0.0;
            Zy = 0.0;
            k: for ( _k = 0; _k < max_steps; ++ _k )
            {
                _Zx = Zx*Zx - Zy*Zy + x;
                _Zy = 2.0*Zx*Zy + y;
                if (_Zx*_Zx + _Zy*_Zy >= max_dist)
                    break k;
                Zx = _Zx;
                Zy = _Zy;

                ++ iteration_count;
            }

            k: switch (_k % max_steps)
            {
            case 9: s += ' '; break;
            case 8: s += '.'; break;
            case 7: s += ':'; break;
            case 6: s += '+'; break;
            case 5: s += '@'; break;
            case 4: s += '='; break;
            case 3: s += '*'; break;
            case 2: s += '%'; break;
            case 1: s += '$'; break;
            default:
                    s += '#';
                    break;
            }
        }

        s += '\n';
    }

    swap(s, render);
};

S := ^\d+ :width, ^\d+ :height {
    width = (width == null) ? 100 : integer(width);
    height = (height == null) ? 49 : integer(height);
    s = render(width, height);
};
